plugins {
    id 'java-gradle-plugin'
    id 'idea'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'signing'
}

group = 'com.mleitz1.quarkus'
version = '0.1.0'
description = 'A diagnostic plugin to help troubleshoot Quarkus builds including Quarkus native'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

gradlePlugin {
    website = 'https://github.com/mikeleitz/quarkus-native-starters-by-mleitz1'
    vcsUrl = 'https://github.com/mikeleitz/quarkus-native-starters-by-mleitz1.git'
    plugins {
        quarkusBuildHelper {
            id = 'com.mleitz1.quarkus.quarkus-build-helper'
            implementationClass = 'com.mleitz1.quarkus.gradle.QuarkusBuildHelperPlugin'
            displayName = 'Quarkus Build Helper Plugin'
            description = 'A Gradle plugin for diagnosing and troubleshooting Quarkus builds, including native image builds'
            tags = ['quarkus', 'native', 'diagnostics', 'build']
        }
    }
}

validatePlugins {
    enableStricterValidation = true
    failOnWarning = true
}

test {
    useJUnitPlatform()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

signing {
    sign publishing.publications
}
